#@TYPE: ess Machine (started from raspberrypi4-64.conf)
#@NAME: RaspberryPi 4 Development Board (64bit)
#@DESCRIPTION: Machine configuration for the RaspberryPi 4 in 64 bits mode

# override the default /etc/hostname (machine name by default)
hostname:pn-base-files = "ess-hostname"

MACHINEOVERRIDES = "raspberrypi4:${MACHINE}"

MACHINE_FEATURES += "pci"
MACHINE_EXTRA_RRECOMMENDS += "\
    linux-firmware-rpidistro-bcm43455 \
    bluez-firmware-rpidistro-bcm4345c0-hcd \
    linux-firmware-rpidistro-bcm43456 \
    bluez-firmware-rpidistro-bcm4345c5-hcd \
"

DEFAULTTUNE = "cortexa72"

require conf/machine/include/arm/armv8a/tune-cortexa72.inc
include conf/machine/include/rpi-base.inc

RPI_KERNEL_DEVICETREE = " \
    broadcom/bcm2711-rpi-4-b.dtb \
    broadcom/bcm2711-rpi-400.dtb \
    broadcom/bcm2711-rpi-cm4.dtb \
"

SDIMG_KERNELIMAGE ?= "kernel8.img"
SERIAL_CONSOLES ?= "115200;ttyS0"

UBOOT_MACHINE = "rpi_arm64_config"

VC4DTBO ?= "vc4-kms-v3d"

# When u-boot is enabled we need to use the "Image" format and the "booti"
# command to load the kernel
KERNEL_IMAGETYPE_UBOOT ?= "Image"
# "zImage" not supported on arm64 and ".gz" images not supported by bootloader yet
KERNEL_IMAGETYPE_DIRECT ?= "Image"
KERNEL_BOOTCMD ?= "booti"

ARMSTUB ?= "armstub8-gic.bin"

# enable debug uart
ENABLE_UART = "1"

# enable spi-dev nodes
ENABLE_SPI_BUS = "1"

# enable i2c-dev nodes
ENABLE_I2C = "1"
# auto-load I2C kernel modules
KERNEL_MODULE_AUTOLOAD:rpi += "i2c-dev"
# i2c-bcm2708 may be the low level driver
#  ... 2708 was the rp1 core, POP'd with DDR became 2835
#  ... 2709 was the rp2 core, POP'd with DDR became 2836
#  ... 2710 was the rp3 core, POP'd with DDR became 2837
#  ... xxxx was the rp4 core, POP'd with DDR became 2711
#  ... review later if needed for actual user space writes
#  ... i see a /sys/module/i2c_bcm2835, 2835 is the rp1 POP'd 2711
#KERNEL_MODULE_AUTOLOAD:rpi += "i2c-dev i2c-bcm2708"

# ess-image.bb specifies ovelayfs-etc IMAGE_FEATURES
#   creates /etc overlayfs in specified mount
#   requires ext4 sd card parition for rw /etc data
OVERLAYFS_ETC_MOUNT_POINT ?= "/data"
OVERLAYFS_ETC_FSTYPE ?= "ext4"
OVERLAYFS_ETC_DEVICE ?= "/dev/mmcblk0p3"

# bsp version installed in and autoloads during kernel init
KERNEL_MODULE_AUTOLOAD += "bsp-version"
